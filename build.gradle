apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
group = "com.victorsima"
archivesBaseName = "uber-java-client"
version = '0.0.2-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.5.1'
    compile 'com.squareup.okhttp:okhttp:1.5.4'
    //compile 'com.netflix.rxjava:rxjava-core:0.17.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
test {
    testLogging {
        events 'started', 'passed'
    }
}
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: ossrhRepositoryUrl) {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: ossrhSnapshotRepositoryUrl) {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Uber java REST Api client'
        packaging 'jar'
        description 'uber-java-client'
        url 'https://github.com/vsima/uber-java-client'

        scm {
          connection 'scm:git:git@github.com:vsima/uber-java-client.git'
          developerConnection 'scm:git:git@github.com:vsima/uber-java-client.git'
          url 'git@github.com:vsima/uber-java-client.git'
        }

        licenses {
          license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id ossrhDevId
            name ossrhDevName
            email ossrhDevEmail
          }
        }
      }
    }
  }
}
